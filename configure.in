
AC_INIT(ncdu, git, projects@yorhel.nl)
AC_CONFIG_SRCDIR([src/global.h])
AC_CONFIG_HEADER([config.h])
AM_INIT_AUTOMAKE

# Check for programs.
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_RANLIB

# Check for header files.
AC_CHECK_HEADERS(
  [limits.h stdlib.h string.h sys/time.h sys/types.h sys/stat.h dirent.h unistd.h fnmatch.h ncurses.h locale.h],[],
  AC_MSG_ERROR([required header file not found]))

# Check for typedefs, structures, and compiler characteristics.
AC_TYPE_OFF_T
AC_SYS_LARGEFILE
AC_STRUCT_ST_BLOCKS
AC_CHECK_TYPE([unsigned long long], [],
              [AC_MSG_ERROR([your compiler does not have an unsigned long long type.])])

# Check for library functions.
AC_CHECK_FUNCS(
  [getcwd gettimeofday memset fnmatch chdir rmdir unlink lstat getcwd setlocale],[],
  AC_MSG_ERROR([required function missing]))



# Look for ncurses library to link to
ncurses=auto
AC_ARG_WITH([ncurses],
            AC_HELP_STRING([--with-ncurses], [compile/link with ncurses library] ),
            [ncurses=ncurses])
AC_ARG_WITH([ncursesw],
            AC_HELP_STRING([--with-ncursesw], [compile/link with wide-char ncurses library @<:@default@:>@]),
            [ncurses=ncursesw])
if test "$ncurses" = "auto"; then
  AC_CHECK_LIB([ncursesw],
               [initscr],
               [ncurses=ncursesw],
               [ncurses=ncurses])
fi
AC_CHECK_LIB([$ncurses],
             [initscr],
             [LIBS="$LIBS -l$ncurses"],
             [AC_MSG_ERROR($ncurses library is required)])



# compll configuration (only zlib is supported currently)
compll=no
AC_ARG_WITH([compll],
            AC_HELP_STRING([--with-compll=zlib], [use zlib for in-memory compression (experimental)]),
            [compll=$withval])
if test "x$compll" != "xno"; then
  AC_CHECK_LIB([z],
               [zlibVersion],
               [AC_CHECK_HEADER([zlib.h], [LIBS="$LIBS -lz"; compll=zlib],
                                [if test "x$compll" = "xauto"; then compll=no
                                 else AC_MSG_ERROR([--with-compll is given, but zlib.h could not be found]); fi])
               ],
               [if test "x$compll" = "xauto"; then compll=no
                else AC_MSG_ERROR([--with-compll is given, but no zlib library could not be found]); fi])
fi
if test "x$compll" = "xno"; then
  AC_DEFINE([COMPLL_NOLIB], [1], [don't use compll])
fi
AM_CONDITIONAL([COMPLL], [test "x$compll" != "xno"])



AC_OUTPUT([Makefile src/Makefile doc/Makefile])

echo ""
echo "Now type \"make\" and \"make install\" to build and install ncdu"
